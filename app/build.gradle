apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.0"
    defaultConfig {
        applicationId "top.huyuxin.multi_product"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            // 混淆
            minifyEnabled true
            // Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            // 前一部分代表系统默认的android程序的混淆文件，该文件已经包含了基本的混淆声明，后一个文件是自己的定义混淆文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {

                        // apk_渠道名-版本号-版本名称-编译时间.apk
                        def fileName = "apk_${variant.productFlavors[0].name}-${defaultConfig.versionCode}-${defaultConfig.versionName}-${releaseTime() }.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }
    productFlavors{
        //        applicationId 对应渠道的包名
        //        对应渠道版本号
        ali{
            applicationId "top.huyuxin.multi_product.ali"
            versionName "version-ali-1.0"
            manifestPlaceholders = [CHANNEL:"ali"]
        }

        tencent{
            applicationId "top.huyuxin.multi_product.tencent"
            versionName "version-tencent-1.0"
            manifestPlaceholders = [CHANNEL:"tencent"]
        }

        baidu{
            applicationId "top.huyuxin.multi_product.baidu"
            versionName "version-baidu-1.0"
            manifestPlaceholders = [CHANNEL:"baidu"]
        }

        xiaomi{
            applicationId "top.huyuxin.multi_product.xiaomi"
            versionName "version-xiaomi-1.0"
            manifestPlaceholders = [CHANNEL:"xiaomi"]
        }
    }
}
def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    testCompile 'junit:junit:4.12'
    aliCompile project(':alisdk')
    baiduCompile project(':baidusdk')
    tencentCompile project(':tencentsdk')
    xiaomiCompile project(':xiaomisdk')
    compile project(path: ':library')
}
